package de.lennard.school.solver
dialect "java"

import org.optaplanner.core.api.score.buildin.hardsoft.HardSoftScoreHolder
import de.lennard.school.domain.Unterricht
import de.lennard.school.domain.Klasse
import de.lennard.school.domain.Raum
import de.lennard.school.domain.Wochentag

global HardSoftScoreHolder scoreHolder


// HARD CONSTRAINTS

// Der Constraint "Jeder Unterricht m√ºssen ein Termin und ein Raum zugeordnet sein." wird implizit
// durch OptaPlanner sichergestellt, da jeder Planungsvariable ein Wert zugewiesen sein muss. 

// Zwei Unterrichten d√ºrfen nicht zeitgleich im gleichen Raum stattfinden.
// Jeder Raumkonflikt z√§hlt als 1 Constraint-Match.
// Fachunterricht muss in Fachr‰uumen sein 
// sport immer doppelt 
//5-9 keine freistunden
rule "RaumKonflikt"
	when
		$v1	: Unterricht(termin != null, raum != null)
		$v2 : Unterricht(id > $v1.id, termin == $v1.termin, raum == $v1.raum)
	then
		scoreHolder.addHardConstraintMatch(kcontext, -1);
end

// Unterrichten des gleichen Klasses d√ºrfen nicht am gleichen Termin stattfinden.
// Jeder Terminkonflikt z√§hlt als 1 Constraint-Match.
rule "TerminKonflikt"
	when
		$v1	: Unterricht(termin != null)
		$v2 : Unterricht(id > $v1.id, termin == $v1.termin, klasse == $v1.klasse)
	then
	   scoreHolder.addHardConstraintMatch(kcontext, -1);
end


// SOFT CONSTRAINTS

// Die Unterrichten eines Klasses sollten im gleichen Raum stattfinden.
// Jeder zus√§tzliche Raum, der f√ºr Unterrichten eines Klasses verwendet wird, z√§hlt als 1 Constraint-Match.
// wenig freistunden
//
rule "RaumKontinuitaet"
	when
		$klasse : Klasse()
		accumulate (
			$raum : Raum() and exists Unterricht(klasse == $klasse, raum == $raum);
			$c : count();
			$c > 1 
		)
	then
	   scoreHolder.addSoftConstraintMatch(kcontext, -($c.intValue() - 1));
end

// Die Unterrichten eines Klasses sollten an m√∂glichst wenigen Wochentagen stattfinden.
// Jeder zus√§tzliche Tag z√§hlt als 1 Constraint-Match.
rule "WochenKompaktheit"
	when
		$klasse : Klasse()
		accumulate (
			$tag : Wochentag() and exists Unterricht(klasse == $klasse, tag == $tag);
			$c : count();
			$c > 1 
		)
	then
	   scoreHolder.addSoftConstraintMatch(kcontext, -($c.intValue() - 1));
end

// Die Unterrichten eines Klasses am gleichen Tag sollten aufeinander folgen; es sollte m√∂glichst wenig Freistunden zwischen zwei Unterrichten geben.
// Jede "isoliert" stattfindende Unterricht z√§hlt als 1 Constraint-Match.
rule "TagKompaktheit"
	when
		$unterricht : Unterricht($klasse : klasse, $tag : tag, $intervallIndex : intervallIndex)

		// Es existiert eine Unterricht des gleichen Klasses am gleichen Tag, die zu einer anderen Zeit stattfindet.
		exists (
			Unterricht(klasse == $klasse, tag == $tag, intervallIndex != $intervallIndex)
		)

		// Es existiert KEINE Unterricht des gleichen Klasses am gleichen Tag, die direkt vor oder nach der aktuellen Unterricht stattfindet.
		not (
			exists (
				Unterricht(klasse == $klasse, tag == $tag, (intervallIndex == $intervallIndex - 1) || (intervallIndex == $intervallIndex + 1))
			)
		)
	then
	   scoreHolder.addSoftConstraintMatch(kcontext, -1);
end